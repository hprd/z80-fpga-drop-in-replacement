`timescale 1ns / 1ps
`include "z80_defines.v"

module ALU_Core(
    input [6:0] ALU_OP,
    input [15:0] operandA,
    input [15:0] operandB,
    input [7:0] flag,
    output reg [15:0] ALU_OUT,
    output reg [7:0] FLAG_OUT
    );
    
    function [15:0] extendTo16;
        input [7:0] num;
        begin
            extendTo16 = {num[7] ? 8'hFF : 8'b0, num};
        end
    endfunction
    
    reg [7:0] TEMP;
    
    wire [7:0] operandB8_plus_carry = operandB[7:0] + flag[`FLAG_C];
    wire [8:0] add_c_8bit_wire = operandA[7:0] + operandB[7:0] + flag[`FLAG_C];
    wire [8:0] add_8bit_wire = operandA[7:0] + operandB[7:0];
    wire [4:0] add_8bit_half = operandA[3:0] + operandB[3:0];
    wire [4:0] add_c_8bit_half = operandA[3:0] + operandB[3:0] + flag[`FLAG_C];
    
    wire add_8bit_H = add_8bit_half[4]; // carry from bit 3
    wire add_c_8bit_H = add_c_8bit_half[4]; // carry from bit 3  
    wire add_8bit_overflow = !(operandA[7] ^ operandB[7]) && (operandA[7] ^ add_8bit_wire[7]);
    wire add_c_8bit_overflow = !(operandA[7] ^ operandB8_plus_carry[7]) && (operandA[7] ^ add_c_8bit_wire[7]);
    
    wire [8:0] sub_c_8bit_wire = operandA[7:0] - (operandB[7:0] + flag[`FLAG_C]);
    wire [8:0] sub_8bit_wire = operandA[7:0] - operandB[7:0];
    
    wire sub_8bit_H = $signed(operandA[3:0]) < $signed(operandB[3:0]);
    wire sub_c_8bit_H = $signed(operandA[3:0]) < $signed(operandB8_plus_carry[3:0]);
    wire sub_8bit_overflow = (operandA[7] ^ operandB[7]) && !(operandA[7] ^ sub_8bit_wire[7]);
    wire sub_c_8bit_overflow = (operandA[7] ^ operandB8_plus_carry[7]) && !(operandA[7] ^ sub_c_8bit_wire[7]);
    
    wire [7:0] and_wire = operandA[7:0] & operandB[7:0];
    wire [7:0] or_wire = operandA[7:0] | operandB[7:0];
    wire [7:0] xor_wire = operandA[7:0] ^ operandB[7:0];
    
    wire [4:0] inc_8bit_half = operandA[3:0] + 1;
    wire inc_8bit_H = inc_8bit_half[4];
    wire [8:0] inc_8bit_wire = operandA[7:0] + 1;
    wire [8:0] dec_8bit_wire = operandA[7:0] - 1;
    
    wire [7:0] ones_8bit_wire = ~operandA[7:0];
    wire [7:0] twos_8bit_wire = -operandA[7:0];
    
    // K-Map solve for BCD DAA instruction
    wire A = flag[`FLAG_C];
    wire B = operandA[7:4] < 4'hA;
    wire C = operandA[7:4] < 4'h9;
    wire D = flag[`FLAG_H];
    wire E = operandA[3:0] < 4'hA;
    wire F = flag[`FLAG_N];
    wire G = operandA[3:0] < 4'h6;
    
    wire high_diff = (A) || (!B && E) || (!C && !E);
    wire low_diff =  (D) || (!E);
    wire new_carry = (A) || (!C && !E) || (!B && E);
    wire new_half_carry = (!F && !E) || (F && D && G);
    
    wire [7:0] BCD;
    assign BCD[7:4] = high_diff ? 6 : 0;
    assign BCD[3:0] = low_diff ? 6 : 0;
    
    wire [7:0] BCD_1 = operandA[7:0] - BCD;
    wire [7:0] BCD_2 = operandA[7:0] + BCD;
    
    wire [7:0] rotate_c_left_wire = {operandA[6:0], flag[`FLAG_C]};
    wire [7:0] rotate_left_wire = {operandA[6:0], operandA[7]};
    wire [7:0] rotate_c_right_wire = {flag[`FLAG_C], operandA[7:1]};
    wire [7:0] rotate_right_wire = {operandA[0], operandA[7:1]};
    
    wire [7:0] shift_left_wire = {operandA[6:0], 1'b0};
    wire [7:0] arithmetic_shift_right_wire = {operandA[7], operandA[7:1]};
    wire [7:0] logical_shift_right_wire = {1'b0, operandA[7:1]};
    
    wire [7:0] TEST_wire = (operandA[7:0] & (1'b1 << (ALU_OP - `ALU_TEST_BASE)));
    wire [7:0] TEST_IX_wire = (operandA[7:0] & (1'b1 << (ALU_OP - (`ALU_TEST_IX_BASE))));
    wire [7:0] TEST_HL_wire = (operandA[7:0] & (1'b1 << (ALU_OP - (`ALU_TEST_HL_BASE))));
    wire [7:0] set_wire = operandA[7:0] | (1'b1 << (ALU_OP - `ALU_SET_BASE));
    wire [7:0] reset_wire = operandA[7:0] & ~(8'b0 + (1'b1 << (ALU_OP - `ALU_RESET_BASE)));
    
    wire [15:0] operandB16_plus_carry = operandB[15:0] + flag[`FLAG_C]; 
    wire [16:0] add_c_16bit_wire = operandA[15:0] + operandB[15:0] + flag[`FLAG_C];
    wire [16:0] add_16bit_wire = operandA[15:0] + operandB[15:0];
    wire [12:0] add_16bit_half = operandA[11:0] + operandB[11:0];
    wire [12:0] add_c_16bit_half = operandA[11:0] + operandB[11:0] + flag[`FLAG_C];
    wire add_16bit_H = add_16bit_half[12];
    wire add_c_16bit_H = add_c_16bit_half[12];
    wire add_c_16bit_overflow = !(operandA[15] ^ operandB16_plus_carry[15]) && (operandA[15] ^ add_c_16bit_wire[15]);
    
    wire [16:0] sub_c_16bit_wire = operandA[15:0] - operandB16_plus_carry[15:0];
    wire sub_c_16bit_H = $signed(operandA[11:0]) < $signed(operandB16_plus_carry[11:0]);
    wire sub_c_16bit_overflow = (operandA[15] ^ operandB16_plus_carry[15]) && !(operandA[15] ^ sub_c_16bit_wire[15]);
    
    wire [15:0] inc_16bit_wire = operandA[15:0] + 1;
    wire [15:0] dec_16bit_wire = operandA[15:0] - 1;
    
    // operandB[15:8] = ACC
    // operandB[7:0] = (HL) or (DE)
    wire [8:0] ldid_flag_add = operandB[15:8] + operandB[7:0];
    
    wire [8:0] cpid_flag_wire = sub_8bit_wire - flag[`FLAG_H];
    
    //  ((HL) + ((C + 1) & 255)
    wire [7:0] ini_HL_C_inc = operandB[7:0] + inc_8bit_wire[7:0];
    wire ini_h_c_flag = ini_HL_C_inc[7:0] > 8'hFF;
    
    //  ((HL) + ((C - 1) & 255)
    wire [7:0] ini_HL_C_dec = operandB[7:0] + dec_8bit_wire[7:0];
    wire ind_h_c_flag = ini_HL_C_dec[7:0] > 8'hFF;
    
    // (((HL) + ((C + 1) & 255)) & 7) xor B)
    wire ini_pv_flag = (ini_HL_C_inc[7:0] & 8'h7) ^ operandB[15:8];
    
    // (((HL) + ((C - 1) & 255)) & 7) xor B)
    wire ind_pv_flag = (ini_HL_C_inc[7:0] & 8'h7) ^ operandB[15:8];
    
    // (HL) + L > 255 (carry happened)
    // {B, (HL)} is on operandA, HL is on operandB
    // L is supposed to be post inc/dec but this must run before that happens
    wire outi_h_c_flag = operandA[7:0] + operandB[7:0] + 1;
    wire outd_h_c_flag = operandA[7:0] + operandB[7:0] - 1;
    
    wire out_pv_flag = (add_8bit_wire[7:0] & 8'h7) ^ operandA[15:8];
    
    always @(ALU_OP, operandA, operandB, flag) begin
        FLAG_OUT[`FLAG_Y] <= flag[`FLAG_Y];
        FLAG_OUT[`FLAG_X] <= flag[`FLAG_X];
        TEMP <= TEMP;
        case (ALU_OP)
            `ALU_ADD_8BIT: begin
                ALU_OUT <= add_8bit_wire[7:0];
                FLAG_OUT[`FLAG_S] <= add_8bit_wire[7];
                FLAG_OUT[`FLAG_Z] <= add_8bit_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= add_8bit_wire[5];
                FLAG_OUT[`FLAG_H] <= add_8bit_H; 
                FLAG_OUT[`FLAG_X] <= add_8bit_wire[3];
                FLAG_OUT[`FLAG_PV] <= add_8bit_overflow; 
                FLAG_OUT[`FLAG_N] <= 1'b0;
                FLAG_OUT[`FLAG_C] <= add_8bit_wire[8]; 
            end
            `ALU_ADC_8BIT: begin
                ALU_OUT <= add_c_8bit_wire[7:0];
                FLAG_OUT[`FLAG_S] <= add_c_8bit_wire[7];
                FLAG_OUT[`FLAG_Z] <= add_c_8bit_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= add_c_8bit_wire[5];
                FLAG_OUT[`FLAG_H] <= add_c_8bit_H;
                FLAG_OUT[`FLAG_X] <= add_c_8bit_wire[3];
                FLAG_OUT[`FLAG_PV] <= add_c_8bit_overflow; 
                FLAG_OUT[`FLAG_N] <= 1'b0;
                FLAG_OUT[`FLAG_C] <= add_c_8bit_wire[8]; 
            end
            `ALU_SUB_8BIT: begin
                ALU_OUT <= sub_8bit_wire[7:0];
                FLAG_OUT[`FLAG_S] <= sub_8bit_wire[7];
                FLAG_OUT[`FLAG_Z] <= sub_8bit_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= sub_8bit_wire[5];
                FLAG_OUT[`FLAG_H] <= sub_8bit_H;
                FLAG_OUT[`FLAG_X] <= sub_8bit_wire[3];
                FLAG_OUT[`FLAG_PV] <= sub_8bit_overflow;
                FLAG_OUT[`FLAG_N] <= 1'b1;
                FLAG_OUT[`FLAG_C] <= $signed(operandA[7:0]) < $signed(operandB[7:0]);
            end
            `ALU_SBC_8BIT: begin
                ALU_OUT <= sub_c_8bit_wire[7:0];
                FLAG_OUT[`FLAG_S] <= sub_c_8bit_wire[7];
                FLAG_OUT[`FLAG_Z] <= sub_c_8bit_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= sub_c_8bit_wire[5];
                FLAG_OUT[`FLAG_H] <= sub_c_8bit_H;
                FLAG_OUT[`FLAG_X] <= sub_c_8bit_wire[3];
                FLAG_OUT[`FLAG_PV] <= sub_c_8bit_overflow;
                FLAG_OUT[`FLAG_N] <= 1'b1;
                FLAG_OUT[`FLAG_C] <= $signed(operandA[7:0]) < $signed(operandB[7:0]);
            end
            `ALU_AND_8BIT: begin
                ALU_OUT <= and_wire;
                FLAG_OUT[`FLAG_S] <= and_wire[7];
                FLAG_OUT[`FLAG_Z] <= and_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= and_wire[5];
                FLAG_OUT[`FLAG_H] <= 1;
                FLAG_OUT[`FLAG_X] <= and_wire[3];
                FLAG_OUT[`FLAG_PV] <= !(^and_wire); // even parity according to P/V subsection
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= 0;
            end
            `ALU_OR_8BIT: begin
                ALU_OUT <= or_wire;
                FLAG_OUT[`FLAG_S] <= or_wire[7];
                FLAG_OUT[`FLAG_Z] <= or_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= or_wire[5];
                FLAG_OUT[`FLAG_H] <= 0;
                FLAG_OUT[`FLAG_X] <= or_wire[3];
                FLAG_OUT[`FLAG_PV] <= !(^or_wire);
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= 0;
            end
            `ALU_XOR_8BIT: begin
                ALU_OUT <= xor_wire;
                FLAG_OUT[`FLAG_S] <= xor_wire[7];
                FLAG_OUT[`FLAG_Z] <= xor_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= xor_wire[5];
                FLAG_OUT[`FLAG_H] <= 0;
                FLAG_OUT[`FLAG_X] <= xor_wire[3];
                FLAG_OUT[`FLAG_PV] <= !(^xor_wire); // even parity
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= 0;
            end
            `ALU_CP: begin
                ALU_OUT <= operandB; // see undocumented 8.4
                FLAG_OUT[`FLAG_S] <= sub_8bit_wire[7];
                FLAG_OUT[`FLAG_Z] <= sub_8bit_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= operandB[5];
                FLAG_OUT[`FLAG_H] <= sub_8bit_H;
                FLAG_OUT[`FLAG_X] <= operandB[3];
                FLAG_OUT[`FLAG_PV] <= sub_8bit_overflow;
                FLAG_OUT[`FLAG_N] <= 1;
                FLAG_OUT[`FLAG_C] <= $signed(operandA[7:0]) < $signed(operandB[7:0]);
            end
            `ALU_INC_8BIT: begin
                ALU_OUT <= inc_8bit_wire;
                FLAG_OUT[`FLAG_S] <= inc_8bit_wire[7];
                FLAG_OUT[`FLAG_Z] <= inc_8bit_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= inc_8bit_wire[5];
                FLAG_OUT[`FLAG_H] <= inc_8bit_H;
                FLAG_OUT[`FLAG_X] <= inc_8bit_wire[3];
                FLAG_OUT[`FLAG_PV] <= operandA[7:0] == 8'h7F;
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_DEC_8BIT: begin
                ALU_OUT <= dec_8bit_wire;
                FLAG_OUT[`FLAG_S] <= dec_8bit_wire[7];
                FLAG_OUT[`FLAG_Z] <= dec_8bit_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= dec_8bit_wire[5];
                FLAG_OUT[`FLAG_H] <= operandB[3:0] == 0;
                FLAG_OUT[`FLAG_X] <= dec_8bit_wire[3];
                FLAG_OUT[`FLAG_PV] <= operandA[7:0] == 8'h80;
                FLAG_OUT[`FLAG_N] <= 1;
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_CPL: begin
                ALU_OUT <= ones_8bit_wire;
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                FLAG_OUT[`FLAG_Y] <= ones_8bit_wire[5];
                FLAG_OUT[`FLAG_H] <= 1;
                FLAG_OUT[`FLAG_X] <= ones_8bit_wire[3];
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= 1;
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_NEG: begin
                ALU_OUT <= twos_8bit_wire;
                FLAG_OUT[`FLAG_S] <= twos_8bit_wire[7];
                FLAG_OUT[`FLAG_Z] <= twos_8bit_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= twos_8bit_wire[5];
                FLAG_OUT[`FLAG_H] <= 0 < $signed(operandA[3:0]); // allegedly
                FLAG_OUT[`FLAG_X] <= twos_8bit_wire[3];
                FLAG_OUT[`FLAG_PV] <= operandA[7:0] == 8'h80;
                FLAG_OUT[`FLAG_N] <= 1;
                FLAG_OUT[`FLAG_C] <= operandA[7:0] == 8'h00;
            end
            `ALU_CCF: begin
                ALU_OUT <= operandA; // The A register must be exposed to operandA for this instruction, see undocumented z80 8.5 for X and Y behavior
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                FLAG_OUT[`FLAG_Y] <= operandA[5];
                FLAG_OUT[`FLAG_H] <= flag[`FLAG_C];
                FLAG_OUT[`FLAG_X] <= operandA[3];
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= !flag[`FLAG_C];
            end
            `ALU_SCF: begin
                ALU_OUT <= operandA; // The A register must be exposed to operandA for this instruction,  see undocumented z80 8.5 for X and Y behavior
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                FLAG_OUT[`FLAG_Y] <= operandA[5];
                FLAG_OUT[`FLAG_H] <= 0;
                FLAG_OUT[`FLAG_X] <= operandA[3];
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= 1;
            end              
            `ALU_DAA: begin
            // page 18 of https://datasheets.chipdb.org/Zilog/Z80/z80-documented-0.90.pdf  
            
                if (flag[`FLAG_N]) begin
                    ALU_OUT <= extendTo16(BCD_1);
                    FLAG_OUT[`FLAG_Y] <= BCD_1[5];
                    FLAG_OUT[`FLAG_X] <= BCD_1[3];
                end else begin
                    ALU_OUT <= extendTo16(BCD_2);
                    FLAG_OUT[`FLAG_Y] <= BCD_2[5];
                    FLAG_OUT[`FLAG_X] <= BCD_2[3];
                end
                    
                FLAG_OUT[`FLAG_S] <= operandA[7];
                FLAG_OUT[`FLAG_Z] <= operandA[7:0] == 0;
                FLAG_OUT[`FLAG_H] <= new_half_carry;
                FLAG_OUT[`FLAG_PV] <= !(^operandA[7:0]);
                FLAG_OUT[`FLAG_N] <= flag[`FLAG_N];
                FLAG_OUT[`FLAG_C] <= new_carry;
                
            end
            `ALU_RLCA: begin
                ALU_OUT <= {8'b0, rotate_left_wire};
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                FLAG_OUT[`FLAG_Y] <= rotate_left_wire[5];
                FLAG_OUT[`FLAG_H] <= 0; 
                FLAG_OUT[`FLAG_X] <= rotate_left_wire[3];
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= operandA[7];
            end
            `ALU_RLA: begin
                ALU_OUT <= {8'b0, rotate_c_left_wire};
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                FLAG_OUT[`FLAG_Y] <= rotate_c_left_wire[5];
                FLAG_OUT[`FLAG_H] <= 0; 
                FLAG_OUT[`FLAG_X] <= rotate_c_left_wire[3];
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= operandA[7];
            end
            `ALU_RLC: begin
                ALU_OUT <= {8'b0, rotate_left_wire};
                FLAG_OUT[`FLAG_S] <= rotate_left_wire[7];
                FLAG_OUT[`FLAG_Z] <= rotate_left_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= rotate_left_wire[5];
                FLAG_OUT[`FLAG_H] <= 0;
                FLAG_OUT[`FLAG_X] <= rotate_left_wire[3];
                FLAG_OUT[`FLAG_PV] <= !(^rotate_left_wire);
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= operandA[7];
            end
            `ALU_RL: begin
                ALU_OUT <= {8'b0, rotate_c_left_wire};
                FLAG_OUT[`FLAG_S] <= rotate_c_left_wire[7];
                FLAG_OUT[`FLAG_Z] <= rotate_c_left_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= rotate_c_left_wire[5];
                FLAG_OUT[`FLAG_H] <= 0;
                FLAG_OUT[`FLAG_X] <= rotate_c_left_wire[3];
                FLAG_OUT[`FLAG_PV] <= !(^rotate_c_left_wire);
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= operandA[7];
            end
            `ALU_RRCA: begin
                ALU_OUT <= {8'b0, rotate_right_wire};
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                FLAG_OUT[`FLAG_Y] <= rotate_right_wire[5];
                FLAG_OUT[`FLAG_H] <= 0;
                FLAG_OUT[`FLAG_X] <= rotate_right_wire[3];
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= operandA[0];
            end
            `ALU_RRA: begin
                ALU_OUT <= {8'b0, rotate_c_right_wire};
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                FLAG_OUT[`FLAG_Y] <= rotate_c_right_wire[5];
                FLAG_OUT[`FLAG_H] <= 0; 
                FLAG_OUT[`FLAG_X] <= rotate_c_right_wire[3];
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= operandA[0];
            end
            `ALU_RRC: begin
                ALU_OUT <= {8'b0, rotate_right_wire};
                FLAG_OUT[`FLAG_S] <= rotate_right_wire[7];
                FLAG_OUT[`FLAG_Z] <= rotate_right_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= rotate_right_wire[5];
                FLAG_OUT[`FLAG_H] <= 0;
                FLAG_OUT[`FLAG_X] <= rotate_right_wire[3];
                FLAG_OUT[`FLAG_PV] <= !(^rotate_right_wire);
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= operandA[0];
            end
            `ALU_RR: begin
                ALU_OUT <= {8'b0, rotate_c_right_wire};
                FLAG_OUT[`FLAG_S] <= rotate_c_right_wire[7];
                FLAG_OUT[`FLAG_Z] <= rotate_c_right_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= rotate_c_right_wire[5];
                FLAG_OUT[`FLAG_H] <= 0;
                FLAG_OUT[`FLAG_X] <= rotate_c_right_wire[3];
                FLAG_OUT[`FLAG_PV] <= !(^rotate_c_right_wire);
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= operandA[0];
            end
            `ALU_SLA: begin
                ALU_OUT <= extendTo16(shift_left_wire);
                FLAG_OUT[`FLAG_S] <= shift_left_wire[7];
                FLAG_OUT[`FLAG_Z] <= shift_left_wire == 0;
                FLAG_OUT[`FLAG_Y] <= shift_left_wire[5];
                FLAG_OUT[`FLAG_H] <= 0;
                FLAG_OUT[`FLAG_X] <= shift_left_wire[3];
                FLAG_OUT[`FLAG_PV] <= !(^shift_left_wire);
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= operandA[7];
            end
            `ALU_SRA: begin
                ALU_OUT <= extendTo16(arithmetic_shift_right_wire);
                FLAG_OUT[`FLAG_S] <= arithmetic_shift_right_wire[7];
                FLAG_OUT[`FLAG_Z] <= arithmetic_shift_right_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= arithmetic_shift_right_wire[5];
                FLAG_OUT[`FLAG_H] <= 0;
                FLAG_OUT[`FLAG_X] <= arithmetic_shift_right_wire[3];
                FLAG_OUT[`FLAG_PV] <= !(^arithmetic_shift_right_wire);
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= operandA[0];
            end
            `ALU_SRL: begin
                ALU_OUT <= extendTo16(logical_shift_right_wire);
                FLAG_OUT[`FLAG_S] <= 0;
                FLAG_OUT[`FLAG_Z] <= logical_shift_right_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= logical_shift_right_wire[5];
                FLAG_OUT[`FLAG_H] <= 0;
                FLAG_OUT[`FLAG_X] <= logical_shift_right_wire[3];
                FLAG_OUT[`FLAG_PV] <= !(^logical_shift_right_wire);
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= operandA[0];
            end
            `ALU_SLL: begin
                ALU_OUT <= extendTo16(shift_left_wire) | 1'b1;
                FLAG_OUT[`FLAG_S] <= shift_left_wire[7];
                FLAG_OUT[`FLAG_Z] <= shift_left_wire == 0;
                FLAG_OUT[`FLAG_Y] <= shift_left_wire[5];
                FLAG_OUT[`FLAG_H] <= 0;
                FLAG_OUT[`FLAG_X] <= shift_left_wire[3];
                FLAG_OUT[`FLAG_PV] <= !(^shift_left_wire);
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= operandA[7];
            end
            `ALU_TEST_BASE, 
            `ALU_TEST_BASE + 1,
            `ALU_TEST_BASE + 2,
            `ALU_TEST_BASE + 3,
            `ALU_TEST_BASE + 4,
            `ALU_TEST_BASE + 5,
            `ALU_TEST_BASE + 6,
            `ALU_TEST_BASE + 7: begin
                ALU_OUT <= {8'b0, TEST_wire};
                FLAG_OUT[`FLAG_S] <= TEST_wire[7]; // see undocumented
                FLAG_OUT[`FLAG_Z] <= TEST_wire[7:0] != 0;
                FLAG_OUT[`FLAG_Y] <= TEST_wire[5]; // this is only for BIT n,r
                FLAG_OUT[`FLAG_H] <= 1;
                FLAG_OUT[`FLAG_X] <= TEST_wire[3];
                FLAG_OUT[`FLAG_PV] <= TEST_wire[7:0] == 0;
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_TEST_IX_BASE, 
            `ALU_TEST_IX_BASE + 1,
            `ALU_TEST_IX_BASE + 2,
            `ALU_TEST_IX_BASE + 3,
            `ALU_TEST_IX_BASE + 4,
            `ALU_TEST_IX_BASE + 5,
            `ALU_TEST_IX_BASE + 6,
            `ALU_TEST_IX_BASE + 7: begin
                // needs the original address of IX + d to be on operandB
                ALU_OUT <= {8'b0, TEST_IX_wire};
                FLAG_OUT[`FLAG_S] <= TEST_IX_wire[7]; // see undocumented
                FLAG_OUT[`FLAG_Z] <= TEST_IX_wire[7:0] != 0;
                FLAG_OUT[`FLAG_Y] <= operandB[13]; // this is only for BIT n, (IX+d)
                FLAG_OUT[`FLAG_H] <= 1;
                FLAG_OUT[`FLAG_X] <= operandB[11];
                FLAG_OUT[`FLAG_PV] <= TEST_IX_wire[7:0] == 0;
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_TEST_HL_BASE, 
            `ALU_TEST_HL_BASE + 1,
            `ALU_TEST_HL_BASE + 2,
            `ALU_TEST_HL_BASE + 3,
            `ALU_TEST_HL_BASE + 4,
            `ALU_TEST_HL_BASE + 5,
            `ALU_TEST_HL_BASE + 6,
            `ALU_TEST_HL_BASE + 7: begin
                // uses TEMP reg which must be updated on ADD HL, xx : LD r, (IX+d) : and  JR d
                ALU_OUT <= {8'b0, TEST_HL_wire};
                FLAG_OUT[`FLAG_S] <= TEST_HL_wire[7]; // see undocumented
                FLAG_OUT[`FLAG_Z] <= TEST_HL_wire[7:0] != 0;
                FLAG_OUT[`FLAG_Y] <= TEMP[5]; // this is only for BIT n, (HL)
                FLAG_OUT[`FLAG_H] <= 1;
                FLAG_OUT[`FLAG_X] <= TEMP[3];
                FLAG_OUT[`FLAG_PV] <= TEST_HL_wire[7:0] == 0;
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_SET_BASE,
            `ALU_SET_BASE + 1,
            `ALU_SET_BASE + 2,
            `ALU_SET_BASE + 3,
            `ALU_SET_BASE + 4,
            `ALU_SET_BASE + 5,
            `ALU_SET_BASE + 6,
            `ALU_SET_BASE + 7: begin
                ALU_OUT <= {8'b0, set_wire};
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                // FLAG_OUT[`FLAG_Y] <= flag[`FLAG_Y];
                FLAG_OUT[`FLAG_H] <= flag[`FLAG_H];
                // FLAG_OUT[`FLAG_X] <= flag[`FLAG_X];
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= flag[`FLAG_N];
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_RESET_BASE,
            `ALU_RESET_BASE + 1,
            `ALU_RESET_BASE + 2,
            `ALU_RESET_BASE + 3,
            `ALU_RESET_BASE + 4,
            `ALU_RESET_BASE + 5,
            `ALU_RESET_BASE + 6,
            `ALU_RESET_BASE + 7: begin
                ALU_OUT <= {8'b0, reset_wire};
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                // FLAG_OUT[`FLAG_Y] <= flag[`FLAG_Y];
                FLAG_OUT[`FLAG_H] <= flag[`FLAG_H]; 
                // FLAG_OUT[`FLAG_X] <= flag[`FLAG_X];
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= flag[`FLAG_N];
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_LD_TEMP: begin
                ALU_OUT <= 0;
                FLAG_OUT <= 0;
                TEMP <= operandA[15:8];
            end
            `ALU_ADD_16BIT: begin
                ALU_OUT <= add_16bit_wire[15:0];
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                FLAG_OUT[`FLAG_Y] <= add_16bit_wire[13];
                FLAG_OUT[`FLAG_H] <= add_16bit_H;
                FLAG_OUT[`FLAG_X] <= add_16bit_wire[11];
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= add_16bit_wire[15]; 
                
                TEMP <= operandA[15:8]; // see undocumented 4.1
            end
            `ALU_ADC_16BIT: begin
                ALU_OUT <= add_c_16bit_wire[15:0]; 
                FLAG_OUT[`FLAG_S] <= add_c_16bit_wire[15];
                FLAG_OUT[`FLAG_Z] <= add_c_16bit_wire[15:0] == 0;
                FLAG_OUT[`FLAG_Y] <= add_c_16bit_wire[13];
                FLAG_OUT[`FLAG_H] <= add_c_16bit_H; 
                FLAG_OUT[`FLAG_X] <= add_c_16bit_wire[11];
                FLAG_OUT[`FLAG_PV] <= add_c_16bit_overflow;
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= add_c_16bit_wire[16];
            end
            `ALU_SBC_16BIT: begin
                ALU_OUT <= sub_c_16bit_wire[15:0];
                FLAG_OUT[`FLAG_S] <= sub_c_16bit_wire[15];
                FLAG_OUT[`FLAG_Z] <= sub_c_16bit_wire[15:0] == 0;
                FLAG_OUT[`FLAG_Y] <= sub_c_16bit_wire[13];
                FLAG_OUT[`FLAG_H] <= sub_c_16bit_H;
                FLAG_OUT[`FLAG_X] <= sub_c_16bit_wire[11];
                FLAG_OUT[`FLAG_PV] <= sub_c_16bit_overflow;
                FLAG_OUT[`FLAG_N] <= 1;
                FLAG_OUT[`FLAG_C] <= $signed(operandA[15:0]) < $signed(operandB16_plus_carry[15:0]);
            end
            `ALU_INC_16BIT: begin
                ALU_OUT <= inc_16bit_wire[15:0];
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                FLAG_OUT[`FLAG_Y] <= flag[`FLAG_Y];
                FLAG_OUT[`FLAG_H] <= flag[`FLAG_H];
                FLAG_OUT[`FLAG_X] <= flag[`FLAG_X];
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= flag[`FLAG_N];
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_DEC_16BIT: begin
                ALU_OUT <= dec_16bit_wire[15:0];
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                FLAG_OUT[`FLAG_Y] <= flag[`FLAG_Y];
                FLAG_OUT[`FLAG_H] <= flag[`FLAG_H];
                FLAG_OUT[`FLAG_X] <= flag[`FLAG_X];
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= flag[`FLAG_N];
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_RLD: begin
                ALU_OUT[15:12] <= operandA[7:4];
                ALU_OUT[11:8] <= operandB[7:4];
                ALU_OUT[7:4] <= operandB[3:0];
                ALU_OUT[3:0] <= operandA[3:0];
                
                FLAG_OUT[`FLAG_S] <= operandA[7];
                FLAG_OUT[`FLAG_Z] <= {operandA[7:4], operandB[7:4]} == 0;
                FLAG_OUT[`FLAG_H] <= 0;
                FLAG_OUT[`FLAG_PV] <= !(^{operandA[7:4], operandB[7:4]});
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_RRD: begin
                ALU_OUT[15:12] <= operandA[7:4];
                ALU_OUT[11:8] <= operandB[3:0];
                ALU_OUT[7:4] <= operandA[3:0];
                ALU_OUT[3:0] <= operandB[7:4];
                
                FLAG_OUT[`FLAG_S] <= operandA[7];
                FLAG_OUT[`FLAG_Z] <= {operandA[7:4], operandB[3:0]} == 0;
                FLAG_OUT[`FLAG_H] <= 0;
                FLAG_OUT[`FLAG_PV] <= !(^{operandA[7:4], operandB[3:0]});
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_PASSA: begin
                ALU_OUT[15:0] <= operandA[15:0];
                FLAG_OUT <= flag;
            end
            `ALU_PASSB: begin
                ALU_OUT[15:0] <= operandB[15:0];
                FLAG_OUT <= flag;
            end
            `ALU_LDI_INC: begin
                // operandA = register to be incremented at the same time
                // operandB[15:8] = ACC
                // operandB[7:0] = (HL) or (DE)
                ALU_OUT <= inc_16bit_wire[15:0];
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                FLAG_OUT[`FLAG_Y] <= ldid_flag_add[1];
                FLAG_OUT[`FLAG_H] <= 0; 
                FLAG_OUT[`FLAG_X] <= ldid_flag_add[3];
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_LDD_DEC: begin
                // operandA = register to be decremented at the same time
                // operandB[15:8] = ACC
                // operandB[7:0] = (HL) or (DE)
                ALU_OUT <= dec_16bit_wire[15:0];
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                FLAG_OUT[`FLAG_Y] <= ldid_flag_add[1];
                FLAG_OUT[`FLAG_H] <= 0; 
                FLAG_OUT[`FLAG_X] <= ldid_flag_add[3];
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_LD_DEC: begin
                // BC must be on operand A
                // spits out BC-1 with the needed flag update
                ALU_OUT <= dec_16bit_wire;
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                FLAG_OUT[`FLAG_Y] <= flag[`FLAG_Y];
                FLAG_OUT[`FLAG_H] <= flag[`FLAG_H]; 
                FLAG_OUT[`FLAG_X] <= flag[`FLAG_X];
                FLAG_OUT[`FLAG_PV] <= dec_16bit_wire == 0;
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_CP_DEC: begin
                // BC must be on operand A
                // spits out BC-1 with the needed flag update
                ALU_OUT <= dec_16bit_wire;
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                FLAG_OUT[`FLAG_Y] <= flag[`FLAG_Y];
                FLAG_OUT[`FLAG_H] <= flag[`FLAG_H]; 
                FLAG_OUT[`FLAG_X] <= flag[`FLAG_X];
                FLAG_OUT[`FLAG_PV] <= dec_16bit_wire == 0;
                FLAG_OUT[`FLAG_N] <= 1;
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_CPID: begin
                // the same as a CP but with extra pizazz
                ALU_OUT <= operandB; // see undocumented 8.4
                FLAG_OUT[`FLAG_S] <= sub_8bit_wire[7];
                FLAG_OUT[`FLAG_Z] <= sub_8bit_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= cpid_flag_wire[1];
                FLAG_OUT[`FLAG_H] <= sub_8bit_H;
                FLAG_OUT[`FLAG_X] <= cpid_flag_wire[3];
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= 1;
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_LDAIR: begin
                // needs IVR1/R on operandA and IFF2 on bit 0 of operandB
                // NOTE: the value of IFF2 needs to be consistent with an interrupt happening during the LD A, I/R instruction
                ALU_OUT <= 0;
                FLAG_OUT[`FLAG_S] <= operandA[7];
                FLAG_OUT[`FLAG_Z] <= operandA == 0;
                FLAG_OUT[`FLAG_Y] <= operandA[5]; // this is unclear from the undocumented pdf
                FLAG_OUT[`FLAG_H] <= 0;
                FLAG_OUT[`FLAG_X] <= operandA[3]; // this is unclear from the undocumented pdf
                FLAG_OUT[`FLAG_PV] <= operandB[0];
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_IN: begin
                ALU_OUT <= operandA;
                FLAG_OUT[`FLAG_S] <= operandA[7];
                FLAG_OUT[`FLAG_Z] <= operandA == 0;
                FLAG_OUT[`FLAG_Y] <= flag[`FLAG_Y];
                FLAG_OUT[`FLAG_H] <= 0; 
                FLAG_OUT[`FLAG_X] <= flag[`FLAG_X];
                FLAG_OUT[`FLAG_PV] <= !(^operandA[7:0]);
                FLAG_OUT[`FLAG_N] <= 0;
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end
            `ALU_INI_DEC: begin
                // BC register pair must be fed into operandA, (HL), so the MDR, must be fed into operandB
                // B is operandA[15:8], C is operandB[7:0]
                ALU_OUT <= operandA[15:8] - 1; 
                FLAG_OUT[`FLAG_S] <= dec_8bit_wire[7];
                FLAG_OUT[`FLAG_Z] <= dec_8bit_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= dec_8bit_wire[5];
                FLAG_OUT[`FLAG_H] <= ini_h_c_flag;
                FLAG_OUT[`FLAG_X] <= dec_8bit_wire[3];
                FLAG_OUT[`FLAG_PV] <= ini_pv_flag;
                FLAG_OUT[`FLAG_N] <= operandB[7];
                FLAG_OUT[`FLAG_C] <= ini_h_c_flag;
            end
            `ALU_IND_DEC: begin
                // BC register pair must be fed into operandA, (HL), so the MDR, must be fed into operandB
                // B is operandA[15:8], C is operandB[7:0]
                ALU_OUT <= operandA[15:8] - 1; 
                FLAG_OUT[`FLAG_S] <= dec_8bit_wire[7];
                FLAG_OUT[`FLAG_Z] <= dec_8bit_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= dec_8bit_wire[5];
                FLAG_OUT[`FLAG_H] <= ind_h_c_flag;
                FLAG_OUT[`FLAG_X] <= dec_8bit_wire[3];
                FLAG_OUT[`FLAG_PV] <= ind_pv_flag;
                FLAG_OUT[`FLAG_N] <= operandB[7];
                FLAG_OUT[`FLAG_C] <= ind_h_c_flag;
            end
            // NOTE: this is making an assumption that the S, Z, Y, and F flags are still
            // updated like DEC B for OTIR and OTDR and not just set to B=0 case the whole time
            `ALU_OUTI_DEC: begin
                // {B, (HL)} is on operandA, L is on operandB
                ALU_OUT <= operandA[15:8] - 1; 
                FLAG_OUT[`FLAG_S] <= dec_8bit_wire[7];
                FLAG_OUT[`FLAG_Z] <= dec_8bit_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= dec_8bit_wire[5];
                FLAG_OUT[`FLAG_H] <= outi_h_c_flag;
                FLAG_OUT[`FLAG_X] <= dec_8bit_wire[3];
                FLAG_OUT[`FLAG_PV] <= out_pv_flag;
                FLAG_OUT[`FLAG_N] <= operandB[7];
                FLAG_OUT[`FLAG_C] <= outi_h_c_flag;
            end
            // NOTE: this is making an assumption that the S, Z, Y, and F flags are still
            // updated like DEC B for OTIR and OTDR and not just set to B=0 case the whole time
            `ALU_OUTD_DEC: begin
                // {B, (HL)} is on operandA, L is on operandB
                ALU_OUT <= operandA[15:8] - 1; 
                FLAG_OUT[`FLAG_S] <= dec_8bit_wire[7];
                FLAG_OUT[`FLAG_Z] <= dec_8bit_wire[7:0] == 0;
                FLAG_OUT[`FLAG_Y] <= dec_8bit_wire[5];
                FLAG_OUT[`FLAG_H] <= outd_h_c_flag;
                FLAG_OUT[`FLAG_X] <= dec_8bit_wire[3];
                FLAG_OUT[`FLAG_PV] <= out_pv_flag;
                FLAG_OUT[`FLAG_N] <= operandB[7];
                FLAG_OUT[`FLAG_C] <= outd_h_c_flag;
            end
            default: begin
                ALU_OUT <= 16'b0; // allegedly later assignments win so the later bit test/set/reset ifs should work
                FLAG_OUT[`FLAG_S] <= flag[`FLAG_S];
                FLAG_OUT[`FLAG_Z] <= flag[`FLAG_Z];
                // FLAG_OUT[`FLAG_Y] <= 0;
                FLAG_OUT[`FLAG_H] <= flag[`FLAG_H]; 
                // FLAG_OUT[`FLAG_X] <= 0;
                FLAG_OUT[`FLAG_PV] <= flag[`FLAG_PV];
                FLAG_OUT[`FLAG_N] <= flag[`FLAG_N];
                FLAG_OUT[`FLAG_C] <= flag[`FLAG_C];
            end         
        endcase
    end  
endmodule


